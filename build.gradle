plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
    id 'com.palantir.git-version' version '0.15.0'
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
version = '0.1.5'
group = project.maven_group

try {
    def details = versionDetails()
    version = details.lastTag.substring(1)
    if (details.commitDistance > 0) {
        version += "+" + details.commitDistance + "." + details.gitHash;
    }
    def ENV = System.getenv()
    if (!ENV.GITHUB_ACTIONS && !ENV.JITPACK) {
        version += details.isCleanTag ? "+local" : "+dirty"
    }
} catch (ex) {
    println ex
}
sourceSets {
    create("testMod") {
        compileClasspath += main.getCompileClasspath()
        runtimeClasspath += main.getRuntimeClasspath()
    }
}
loom {
    runs {
        create("gametest") {
            server()
            name("Game Test")
            ideConfigGenerated(true)
            source(sourceSets.getByName("testMod"))
            vmArg("-Dfabric-api.gametest")
            runDir("build/test")
        }
    }
}
tasks.getByName("test").dependsOn("runGameTest")
repositories {
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
        mavenContent { snapshotsOnly() }
    }
    maven {
        url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
        mavenContent { snapshotsOnly() }
    }
    mavenCentral()
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.

}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }


    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}
